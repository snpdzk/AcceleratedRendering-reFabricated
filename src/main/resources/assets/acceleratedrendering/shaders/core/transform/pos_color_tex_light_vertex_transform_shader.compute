#version 460 core

struct Vertex {
    float x;
    float y;
    float z;
    uint color;
    float u0;
    float v0;
    uint uv2;
};

struct VaryingData {
    int offset;
    int sharing;
    int mesh;
    int flags;
};

struct SharingData {
    mat4 transform;
    mat3 normal;
};

layout(local_size_x = 128) in;

layout(binding=0, std430) readonly buffer VerticesIn {
    Vertex verticesIn[];
};

layout(binding=1, std430) writeonly buffer VerticesOut {
    Vertex verticesOut[];
};

layout(binding=2, std430) readonly buffer Sharings {
    SharingData sharings[];
};

layout(binding=3, std430) readonly buffer Varyings {
    VaryingData varyings[];
};

layout(binding=4, std430) readonly buffer Meshes {
    Vertex meshVertices[];
};

layout(location=0) uniform uint vertexCount;
layout(location=1) uniform uint vertexOffset;

void main() {
    uint indexIn = gl_GlobalInvocationID.x;
    uint indexOut = indexIn + vertexOffset;

    if (indexIn >= vertexCount) {
        return;
    }

    int offset = varyings[indexIn].offset;
    uint reference = indexIn - offset;
    int sharing = varyings[reference].sharing;
    int mesh = varyings[reference].mesh;

    mat4 transformMatrix;
    // mat3 normalMatrix;

    vec4 pos;
    vec4 color;
    vec4 refColor;
    vec2 uv0;
    uvec2 uv2;
    uvec2 refUv2;
    vec3 normal;

    if (sharing != -1) {
        transformMatrix = sharings[sharing].transform;
        // normalMatrix = sharings[sharing].normal;
    } else {
        transformMatrix = mat4(1.0);
        // normalMatrix = mat3(1.0);
    }

    if (mesh != -1) {
        uint meshIndex = mesh + offset;
        pos = vec4(meshVertices[meshIndex].x, meshVertices[meshIndex].y, meshVertices[meshIndex].z, 1.0);
        color = unpackUnorm4x8(meshVertices[meshIndex].color);
        refColor = unpackUnorm4x8(verticesIn[reference].color);
        uv0 = vec2(meshVertices[meshIndex].u0, meshVertices[meshIndex].v0);
        uv2 = floatBitsToUint(unpackSnorm2x16(meshVertices[meshIndex].uv2));
        refUv2 = floatBitsToUint(unpackSnorm2x16(verticesIn[reference].uv2));
        // normal = unpackSnorm4x8(meshVertices[meshIndex].normal).xyz;
    } else {
        pos = vec4(verticesIn[indexIn].x, verticesIn[indexIn].y, verticesIn[indexIn].z, 1.0);
        color = unpackUnorm4x8(verticesIn[indexIn].color);
        refColor = vec4(1.0);
        uv0 = vec2(verticesIn[indexIn].u0, verticesIn[indexIn].v0);
        uv2 = floatBitsToUint(unpackSnorm2x16(verticesIn[indexIn].uv2));
        refUv2 = uvec2(0u);
        // normal = unpackSnorm4x8(verticesIn[indexIn].normal).xyz;
    }

    pos = transformMatrix * pos;
    color = refColor * color;
    // normal = normalize(normalMatrix * normal);

    verticesOut[indexOut].x = pos.x;
    verticesOut[indexOut].y = pos.y;
    verticesOut[indexOut].z = pos.z;

    verticesOut[indexOut].u0 = uv0.x;
    verticesOut[indexOut].v0 = uv0.y;

    verticesOut[indexOut].color = packUnorm4x8(color);
    // verticesOut[indexOut].uv1 = verticesIn[reference].uv1;
    verticesOut[indexOut].uv2 = packSnorm2x16(uintBitsToFloat(max(uv2, refUv2)));

    // verticesOut[indexOut].normal = packSnorm4x8(vec4(normal, 0.0));
}