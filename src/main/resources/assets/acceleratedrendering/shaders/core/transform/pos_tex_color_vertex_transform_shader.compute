#version 460 core

struct Vertex {
    float x;
    float y;
    float z;
    float u0;
    float v0;
    int color;
};

struct VaryingData {
    int offset;
    int sharing;
    int color;
    int light;
    int overlay;
};

struct SharingData {
    mat4 transform;
    mat3 normal;
    uint flags;
    int mesh;
    uint extra_data_1;
    uint extra_data_2;
};

layout(local_size_x = 128) in;

layout(binding=0, std430) buffer VerticesIn {
    Vertex verticesIn[];
};

layout(binding=1, std430) buffer VerticesOut {
    Vertex verticesOut[];
};

layout(binding=2, std430) readonly buffer Sharings {
    SharingData sharings[];
};

layout(binding=3, std430) readonly buffer Varyings {
    VaryingData varyings[];
};

layout(binding=4, std430) readonly buffer Mesh {
    Vertex meshVertices[];
};

layout(location=0) uniform uint vertexCount;

void main() {
    uint index = gl_GlobalInvocationID.x;

    if (index >= vertexCount) {
        return;
    }

    int offset = varyings[index].offset;

    VaryingData varyingData = varyings[index - offset];
    SharingData sharingData = varyingData.sharing != -1 ? sharings[varyingData.sharing] : SharingData(mat4(1.0), mat3(1.0), 0u, -1, 0u, 0u);
    Vertex vertex = sharingData.mesh != -1 ? meshVertices[sharingData.mesh + offset] : verticesIn[index];

    vec4 pos = vec4(vertex.x, vertex.y, vertex.z, 1.0);
    // vec3 normal = unpackSnorm4x8(vertex.normal).xyz;

    pos = sharingData.transform * pos;
    // normal = normalize(sharingData.normal * normal);

    verticesOut[index].x = pos.x;
    verticesOut[index].y = pos.y;
    verticesOut[index].z = pos.z;

    verticesOut[index].u0 = vertex.u0;
    verticesOut[index].v0 = vertex.v0;

    verticesOut[index].color = varyingData.color;
    // verticesOut[index].uv1 = varyingData.overlay;
    // verticesOut[index].uv2 = varyingData.light;

    // verticesOut[index].normal = packSnorm4x8(vec4(normal, 0.0));
}