#version 460 core

struct Vertex {
    float x;
    float y;
    float z;
    int color;
    float u0;
    float v0;
    int uv1;
    int uv2;
    uint normal;
    uint iris_entity_0;
    uint iris_entity_1;
    float iris_midcoord_u;
    float iris_midcoord_v;
    uint iris_at_tangent;
};

struct Polygon {
    int vertex1;
    int vertex2;
    int vertex3;
    int vertex4;
    int vertex5;
    int vertex6;
};

layout(local_size_x = 128) in;

layout(binding=1, std430) buffer Vertices {
    Vertex vertices[];
};

layout(binding=5, std430) readonly buffer Polygons {
    Polygon polygons[];
};

layout(location=0) uniform uint polygonCount;

void main() {
    uint index = gl_GlobalInvocationID.x;

    if (index >= polygonCount) {
        return;
    }

    Polygon polygon = polygons[index];
    Vertex vertex1 = vertices[polygon.vertex1];
    Vertex vertex2 = vertices[polygon.vertex2];
    Vertex vertex3 = vertices[polygon.vertex3];
    Vertex vertex4 = vertices[polygon.vertex5];

    vec3 pos1 = vec3(vertex1.x, vertex1.y, vertex1.z);
    vec3 pos2 = vec3(vertex2.x, vertex2.y, vertex2.z);
    vec3 pos3 = vec3(vertex3.x, vertex3.y, vertex3.z);
    vec2 uv1 = vec2(vertex1.u0, vertex1.v0);
    vec2 uv2 = vec2(vertex2.u0, vertex2.v0);
    vec2 uv3 = vec2(vertex3.u0, vertex3.v0);
    vec2 uv4 = vec2(vertex4.u0, vertex4.v0);
    vec2 midUV = (uv1 + uv2 + uv3 + uv4) / 4;
    vec3 normal = unpackSnorm4x8(vertex1.normal).xyz;

    vec3 edge1 = pos2 - pos1;
    vec3 edge2 = pos3 - pos1;

    vec2 dUV1 = uv2 - uv1;
    vec2 dUV2 = uv3 - uv1;

    float fdenom = dUV1.x * dUV2.y - dUV2.x * dUV1.y;
    float f = fdenom == 0.0 ? 1.0 : 1.0 / fdenom;

    vec3 tangent = f * (dUV2.y * edge1 - dUV1.y * edge2);
    vec3 bitangent = f * (dUV2.x * edge1 - dUV1.x * edge2);
    vec3 pBitangent = cross(tangent, normal);
    vec4 tangent4 = vec4(tangent, dot(bitangent, pBitangent) < 0.0 ? -1.0 : 1.0);
    uint packedTangent = packSnorm4x8(tangent4);

    vertices[polygon.vertex1].iris_at_tangent = packedTangent;
    vertices[polygon.vertex2].iris_at_tangent = packedTangent;
    vertices[polygon.vertex3].iris_at_tangent = packedTangent;
    vertices[polygon.vertex4].iris_at_tangent = packedTangent;
    vertices[polygon.vertex5].iris_at_tangent = packedTangent;
    vertices[polygon.vertex6].iris_at_tangent = packedTangent;

    vertices[polygon.vertex1].iris_midcoord_u = midUV.x;
    vertices[polygon.vertex2].iris_midcoord_u = midUV.x;
    vertices[polygon.vertex3].iris_midcoord_u = midUV.x;
    vertices[polygon.vertex4].iris_midcoord_u = midUV.x;
    vertices[polygon.vertex5].iris_midcoord_u = midUV.x;
    vertices[polygon.vertex6].iris_midcoord_u = midUV.x;

    vertices[polygon.vertex1].iris_midcoord_v = midUV.y;
    vertices[polygon.vertex2].iris_midcoord_v = midUV.y;
    vertices[polygon.vertex3].iris_midcoord_v = midUV.y;
    vertices[polygon.vertex4].iris_midcoord_v = midUV.y;
    vertices[polygon.vertex5].iris_midcoord_v = midUV.y;
    vertices[polygon.vertex6].iris_midcoord_v = midUV.y;
}